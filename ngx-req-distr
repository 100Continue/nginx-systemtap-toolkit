#!/usr/bin/env perl

use 5.006001;
use strict;
use warnings;

use Getopt::Std qw( getopts );

my %opts;

getopts("a:dhm:", \%opts)
    or die usage();

if ($opts{h}) {
    print usage();
    exit;
}

my $master_pid = $opts{m}
    or die "No nginx master process pid specified by the -m option\n";

if ($master_pid !~ /^\d+$/) {
    die "Bad -m option value \"$master_pid\": not look like a pid\n";
}

my $stap_args = $opts{a} || '';

if ($^O ne 'linux') {
    die "Only linux is supported but I am on $^O.\n";
}

my $exec_file = "/proc/$master_pid/exe";
if (!-f $exec_file) {
    die "Nginx process $master_pid is not running.\n";
}

my $nginx_path = readlink $exec_file;

my $ver = `stap --version 2>&1`;
if (!defined $ver) {
    die "Systemtap not installed or its \"stap\" utility is not visible to the PATH environment: $!\n";
}

if ($ver =~ /version\s+(\d+\.\d+)/i) {
    my $v = $1;
    if ($v < 1.8) {
        die "ERROR: at least systemtap 1.8 is required but found $v\n";
    }

} else {
    die "ERROR: unknown version of systemtap:\n$ver\n";
}

my $child_pids = get_child_processes($master_pid);
if (!@$child_pids) {
    push @$child_pids, $master_pid;
}

my $condition = gen_pid_test_condition($child_pids);

#warn "Nginx worker processes: @$child_pids\n";

my $stap_src;

my $preamble = <<_EOC_;
probe begin {
    printf("Tracing @$child_pids ($nginx_path)...\\nHit Ctrl-C to end.\\n")
}
_EOC_

$stap_src = <<_EOC_;
$preamble

global hits

probe process("$nginx_path").function("ngx_http_init_request")
{
    #printf("pid %d\\n", pid())

    if ($condition) {
        hits[pid()] <<< 1
    }
}

probe end {
    printf("\\n")
    foreach (k in hits+) {
        printf("worker %d:\t\t%d\\n", k, \@count(hits[k]))
    }
}
_EOC_

if ($opts{d}) {
    print $stap_src;
    exit;
}

open my $in, "|stap $stap_args -"
    or die "Cannot run stap: $!\n";

print $in $stap_src;

close $in;

sub usage {
    return <<'_EOC_';
Usage:
    ngx-req-distr [optoins]

Options:
    -a <args>           extra arguments to the stap utility.
    -d                  Dump out the systemtap script source.
    -h                  Print this usage.
    -m <pid>            Specify the nginx master process pid.

Examples:
    ngx-req-distr -p 12345
    ngx-req-distr -p 12345 -a '-DMAXACTION=100000'
_EOC_
}

sub quote_str {
    my $s = shift;
    $s =~ s/\\/\\\\/g;
    $s =~ s/"/\\"/g;
    $s =~ s/\n/\\n/g;
    $s =~ s/\t/\\t/g;
    $s =~ s/\r/\\r/g;
    return qq{"$s"};
}

sub get_child_processes {
    my $pid = shift;
    my @files = glob "/proc/[0-9]*/stat";
    my @children;
    for my $file (@files) {
        #print "file: $file\n";
        if ($file =~ m{^/proc/$pid/}) {
            next;
        }

        open my $in, $file or
            die "Cannot open $file for reading: $!\n";
        my $line = <$in>;
        close $in;
        if ($line =~ /^(\d+) \S+ \S+ (\d+)/) {
            my ($child, $parent) = ($1, $2);
            if ($parent eq $pid) {
                push @children, $child;
            }
        }
    }

    return \@children;
}

sub gen_pid_test_condition {
    my $pids = shift;
    my @c;
    for my $pid (@$pids) {
        push @c, "pid() == $pid";
    }
    return join " || ", @c;
}

